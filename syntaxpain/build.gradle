plugins {
    id "java"
    id "java-library"
    id "maven-publish"
}

group = 'org.quiltmc'
version = 'internal'

final GENERATED_SOURCES = layout.buildDirectory.dir('generated/sources/main')

configurations {
	jflexClasspath
}

sourceSets.main.java.srcDir(GENERATED_SOURCES)

repositories {
	mavenCentral()
}

dependencies {
	jflexClasspath 'de.jflex:jflex:1.9.1'
}

ant.taskdef(
	name: 'jflex',
	classname: 'jflex.anttask.JFlexTask',
	classpath: configurations.jflexClasspath.asPath
)

tasks.register('jflex') {
	final input = file('src/main/jflex/zest.jflex')

	inputs.file(input)
	outputs.dir(GENERATED_SOURCES)

	doLast {
		ant.jflex(
			file: input,
			destdir: GENERATED_SOURCES.get()
		)
	}
}

compileJava.dependsOn(':jflex')

java {
	withSourcesJar()
	sourcesJar.dependsOn(':jflex')
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	it.options.release = 17
}
